# Maintainer: Tomasz Zok <tomasz(dot)zok(at)gmail(dot)com>
# Contributor: KafCoppelia <k740677208@gmail.com>

_pkgbase=rtl8852au
pkgname=rtl8852au-dkms-git
pkgver=r100.865ab0f
pkgrel=1
pkgdesc="The WiFi driver for Realtek chipsets RTL8832AU and RTL8852AU (e.g. ASUS USB-AX56, D-Link DW-AX1850, TP-Link AX1800) "
arch=('x86_64')
url="https://github.com/lwfinger/rtl8852au"
license=('GPL2')
depends=('dkms' 'bc' 'linux-headers' 'usb_modeswitch')
makedepends=('git')
conflicts=("${_pkgbase}")
source=('git+https://github.com/lwfinger/rtl8852au'
	'rtl8852au-linux613.patch'
        '40-rtl8852au-usb-modeswitch.rules')
sha512sums=('SKIP'
	    '4c1cd979d70c1039af06c9c3e2d9fd62f22fe6a915a98f357192536493f5d4049ae51b873bfed048b364f0d3fa05c49e28c35adc8a9e08cfadc0470e51b596b9'
            'be2395e48f4b3317aad23ad4f7e64bbb02be733ccd06d3cb95529d6cc22f99fdde9d7c6ea45201fb1cbcc51d5cb87d34ea9e2fb5067faf1093c2c52fdd04eb89')

pkgver() {
  cd "${_pkgbase}"
  ( set -o pipefail
    git describe --long --abbrev=7 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
  )
}

prepare() {
  patch -Np1 -i ../rtl8852au-linux613.patch -d "${srcdir}/${_pkgbase}"
}

package() {
  # Copy dkms.conf
  install -Dm644 "${srcdir}/${_pkgbase}"/dkms.conf "${pkgdir}"/usr/src/${_pkgbase}-${pkgver}/dkms.conf

  # Copy sources (including Makefile)
  cp -r "${srcdir}/${_pkgbase}"/* "${pkgdir}"/usr/src/"${_pkgbase}-${pkgver}"/

  # Copy udev rule preventing from showing the USB dongle as mass storage
  install -Dm644 40-rtl8852au-usb-modeswitch.rules "${pkgdir}"/etc/udev/rules.d/40-rtl8852au-usb-modeswitch.rules
}

