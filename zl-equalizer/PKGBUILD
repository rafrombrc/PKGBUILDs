# Maintainer: Andrej RadoviÄ‡ <r.andrej@gmail.com>
#
pkgname=zl-equalizer
_pkgname=ZLEqualizer
pkgver=0.6.0
pkgrel=1
pkgdesc="Advanced parametric equalizer audio plugin by ZL-Audio, including dynamic EQ"
arch=(any)
url="https://github.com/ZL-Audio/ZLEqualizer"
license=('AGPL3')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'git' 'libx11' 'libxrandr' 'libxinerama' 'libxext' 'libxrender' 'libxcomposite'
	     'libxcursor' 'freetype2' 'fontconfig' 'alsa-lib' 'jack2')
provides=('zlequalizer')
conflicts=('zlequalizer')
options=(!strip)

source=("${_pkgname}-${pkgver}-${pkgrel}::git+https://github.com/ZL-Audio/ZLEqualizer#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
    patch "${srcdir}/${_pkgname}-${pkgver}-${pkgrel}/CMakeLists.txt" ../CMakeLists.txt.patch
}

build() {
    cd ${_pkgname}-${pkgver}-${pkgrel}
    git submodule update --init
    cmake -B cmake-build -DCMAKE_PREFIX_PATH=./juce
    cmake --build cmake-build
}

package() {
    # Create directories
    install -dm755 "${pkgdir}/usr/lib/vst3"
    install -dm755 "${pkgdir}/usr/lib/lv2"

    # Install VST3 plugin
    cp -a "${srcdir}/${_pkgname}-${pkgver}-${pkgrel}/cmake-build/ZLEqualizer_artefacts/VST3/ZL Equalizer.vst3" "${pkgdir}/usr/lib/vst3/ZL Equalizer.vst3"

    # Install LV2 plugin if available
    cp -a "${srcdir}/${_pkgname}-${pkgver}-${pkgrel}/cmake-build/ZLEqualizer_artefacts/LV2/ZL Equalizer.lv2" "${pkgdir}/usr/lib/lv2/ZL Equalizer.lv2"

    # Install license
    install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-${pkgrel}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
