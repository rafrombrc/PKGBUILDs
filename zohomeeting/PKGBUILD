_pkgname=zohomeeting
pkgname="${_pkgname}"-appimage
pkgver=4.1.0
pkgrel=1
pkgdesc="Zoho Meeting desktop client application"
arch=('x86_64')
url="https://www.zoho.com/meeting/"
license=('custom')
depends=('desktop-file-utils')
options=(!debug !strip)
_appimage="${_pkgname}-${pkgver}.AppImage"
source_x86_64=("${_appimage}::https://d1ctjwn89mb7yf.cloudfront.net/agent/4.1.0/ZohoMeeting-x64.AppImage")
noextract=("${_appimage}")
sha512sums_x86_64=('be011e9469b55bfde0e0e9b77be6aacd75616bcd75961b28dcaafb32067b397d2689d044380bd151da96b7366822f1bf06c21eb716b9312c504dbaf9b8ba7458')

prepare() {
    chmod +x "${_appimage}"
    ./"${_appimage}" --appimage-extract > /dev/null
    sed "s|AppRun|/usr/bin/${_pkgname}|" squashfs-root/zohomeeting.desktop > "${_pkgname}".desktop
    cp squashfs-root/zohomeeting.png .
    rm -rf squashfs-root
}

package(){
    install -Dvm755 "${srcdir}/${_appimage}" "${pkgdir}/opt/${_pkgname}/${_pkgname}.AppImage"
    install -Dvm644 zohomeeting.png -t "${pkgdir}/usr/share/icons/"
    desktop-file-install --dir="${pkgdir}/usr/share/applications" zohomeeting.desktop
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${_pkgname}/${_pkgname}.AppImage" "${pkgdir}/usr/bin/${_pkgname}"
}
